plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'kotlin-parcelize'
    id 'androidx.navigation.safeargs.kotlin'
    id 'com.google.gms.google-services' // Google Services plugin
    id 'dagger.hilt.android.plugin'
    id 'com.google.firebase.crashlytics'
    id 'com.google.firebase.firebase-perf'
    id 'applovin-quality-service'
}

applovin {
    apiKey APPLOVIN_API_KEY
}

android {
    compileSdk 33
    buildToolsVersion '33.0.0'

    defaultConfig {
        applicationId "com.xeniac.warrantyroster_manager"
        minSdkVersion 21
        targetSdk 33
        versionCode 12
        versionName "1.4.2"

        buildConfigField("String", "APPLOVIN_API_KEY", APPLOVIN_API_KEY)
        buildConfigField("String", "APPLOVIN_INTERSTITIAL_UNIT_ID", APPLOVIN_INTERSTITIAL_UNIT_ID)
        buildConfigField("String", "APPLOVIN_SETTINGS_NATIVE_UNIT_ID", APPLOVIN_SETTINGS_NATIVE_UNIT_ID)
        buildConfigField("String", "APPLOVIN_WARRANTIES_NATIVE_UNIT_ID", APPLOVIN_WARRANTIES_NATIVE_UNIT_ID)
        buildConfigField("String", "TAPSELL_KEY", TAPSELL_KEY)
        buildConfigField("String", "TAPSELL_INTERSTITIAL_ZONE_ID", TAPSELL_INTERSTITIAL_ZONE_ID)
        buildConfigField("String", "TAPSELL_WARRANTIES_NATIVE_ZONE_ID", TAPSELL_WARRANTIES_NATIVE_ZONE_ID)
        buildConfigField("String", "TAPSELL_SETTINGS_NATIVE_ZONE_ID", TAPSELL_SETTINGS_NATIVE_ZONE_ID)

        testInstrumentationRunner "com.xeniac.warrantyroster_manager.HiltTestRunner"
    }

    buildFeatures {
        viewBinding true
        dataBinding true
    }

    buildTypes {
        release {
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    bundle {
        language {
            /**
             * Specifies that the app bundle should not support configuration APKs for language resources.
             * These resources are instead packaged with each base and dynamic feature APK.
             */
            enableSplit false
        }
    }
}

kapt {
    correctErrorTypes = true
}

dependencies {
    implementation 'androidx.core:core-ktx:1.8.0'
    implementation 'androidx.appcompat:appcompat:1.4.2'
    implementation 'com.google.android.material:material:1.6.1'
    implementation 'androidx.core:core-splashscreen:1.0.0-rc01'

    // Navigation Component
    implementation 'androidx.navigation:navigation-fragment-ktx:2.5.0'
    implementation 'androidx.navigation:navigation-ui-ktx:2.5.0'

    // Dagger - Hilt
    implementation 'com.google.dagger:hilt-android:2.42'
    kapt 'com.google.dagger:hilt-compiler:2.42'

    // Activity KTX for Injecting ViewModels into Fragments
    implementation 'androidx.activity:activity-ktx:1.5.0'

    // Architectural Components
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.5.0'
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.5.0'

    // Coroutines
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.6.3'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.6.3'

    // Coroutines Support for Firebase
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-play-services:1.6.3'

    // Preferences DataStore
    implementation 'androidx.datastore:datastore-preferences:1.0.0'

    // Firebase BoM and Analytics
    implementation platform('com.google.firebase:firebase-bom:30.1.0')
    implementation 'com.google.firebase:firebase-analytics-ktx'

    // Firebase App Check
    implementation 'com.google.firebase:firebase-appcheck-playintegrity'
    implementation 'com.google.firebase:firebase-appcheck-debug'

    // Firebase Release & Monitor
    implementation 'com.google.firebase:firebase-crashlytics-ktx'
    implementation 'com.google.firebase:firebase-perf-ktx'

    // Firebase Auth, Firestore, Storage
    implementation 'com.google.firebase:firebase-auth-ktx'
    implementation 'com.google.firebase:firebase-firestore-ktx'
    implementation 'com.google.firebase:firebase-storage-ktx'

    // Timber Library
    implementation 'com.jakewharton.timber:timber:5.0.1'

    // Lottie Library
    implementation 'com.airbnb.android:lottie:5.2.0'

    // Coil Library
    implementation 'io.coil-kt:coil:2.1.0'
    implementation 'io.coil-kt:coil-svg:2.1.0'

    // AppLovin Libraries
    implementation 'com.applovin:applovin-sdk:11.4.3'
    implementation 'com.google.android.gms:play-services-ads-identifier:18.0.1'
    implementation 'com.applovin.mediation:adcolony-adapter:4.8.0.0'
    implementation 'com.applovin.mediation:google-adapter:21.0.0.0'

    // Google AdMob Library
    implementation 'com.google.android.gms:play-services-ads:21.0.0'

    // AdColony Libraries
    implementation 'com.adcolony:sdk:4.8.0'

    // Tapsell Library
    implementation 'ir.tapsell.plus:tapsell-plus-sdk-android:2.1.7'

    // Local Unit Test Libraries
    testImplementation 'com.google.truth:truth:1.1.3'
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'androidx.arch.core:core-testing:2.1.0'
    testImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-test:1.6.3'

    // Instrumentation Test Libraries
    androidTestImplementation 'com.google.truth:truth:1.1.3'
    androidTestImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test:core:1.4.0'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.arch.core:core-testing:2.1.0'
    androidTestImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-test:1.6.3'
    androidTestImplementation 'androidx.navigation:navigation-testing:2.5.0'
    // TODO UPGRADE ESPRESSO TO 3.5.0 AND NOT 3.4.0
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
    androidTestImplementation 'androidx.test.espresso:espresso-contrib:3.3.0'
    androidTestImplementation 'androidx.test.espresso:espresso-intents:3.3.0'
    androidTestImplementation 'com.google.dagger:hilt-android-testing:2.42'
    kaptAndroidTest 'com.google.dagger:hilt-compiler:2.42'
    debugImplementation 'androidx.fragment:fragment-testing:1.5.0'
}